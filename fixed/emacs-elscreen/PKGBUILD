# Contributor: Ismael Barros (RazZziel) <razielmine@gmail.com>
pkgname=emacs-elscreen
pkgver=1.4.6
pkgrel=3
pkgdesc="Frame configuration management for GNU Emacs modelled after GNU Screen"
arch=('any')
url="http://www.morishima.net/~naoto/software/elscreen/index.php.en"
license=('GPL')
depends=('emacs')
makedepends=('emacs-apel')
provides=('emacs-elscreen')
conflicts=('emacs-elscreen')
install=$pkgname.install
source=(ftp://ftp.morishima.net/pub/morishima.net/naoto/ElScreen/elscreen-$pkgver.tar.gz
    03_fix_emacs-command-line.dpatch
)
md5sums=('67d6b8913957ccacefef09c549a939fd'
         '6cfb4d4926576e0aeb2ba59cbf5015ac')

build() {
  cd $srcdir/elscreen-$pkgver

  # Apply patch to fix
  # http://bugs.debian.org/cgi-bin/bugreport.cgi?msg=10;bug=581935
  #
  # The bug prevents emacs from opening a file with the message
  #     "Symbol's value as variable is void: dir"
  # . See patch for details.
  patch -p1 -i $srcdir/03_fix_emacs-command-line.dpatch

  emacs --batch --eval '(progn
			(push "." load-path)
			(byte-compile-file "elscreen.el"))'
}

package() {
  install -d $pkgdir/usr/share/emacs/site-lisp
  cp $srcdir/elscreen-$pkgver/elscreen.el{,c} $pkgdir/usr/share/emacs/site-lisp
}
